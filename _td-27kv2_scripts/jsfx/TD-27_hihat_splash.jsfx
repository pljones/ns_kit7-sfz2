desc: TD-27 Hi-hat Splash detector OMNI (pljones)
author: Peter L Jones
version: 1.0
about: Detects direction changes in CC4 and emits a configurable "hi-hat splash" note. Passes through CC4 but blocks incoming splash note number.
source: https://raw.githubusercontent.com/pljones/ns_kit7-sfz2/refs/heads/main/_td-27kv2_scripts/jsfx/TD-27_hihat_splash.jsfx

slider1:splashNote=97<0,127,1>Hi-hat splash MIDI note to emit
slider2:closedSide=1<0,1,1{Keyboard (0=closed),eKit (127=closed)}>CC4 Hi-hat type
slider3:closedPoint=96<0,127,1>CC4 Closed threshold
slider4:velScale=4<2,64,0.2>Delta-CC4 scaling factor (to splash velocity)
slider5:dampScale=0.6<0,64,0.1>Delta-CC4 damping factor (velocity reduction)
slider6:dbgSplashVel=-1<-1,127,1>Splash velocity

@init
prev_cc4 = -1;     // Need to have seen one before can calculate direction or delta
prev_closing = -1; // Need to have computed a direction before emitting a note
extreme = -1;      // Store fastest throw of pedal
delta = 0;         // Default to not moving
splash_ch = -1;    // Not ready to send a splash yet
overshoot = -1;    // Pedal has not reached splash point

@block
// Process incoming MIDI messages
while (midirecv(offset, msg1, msg2, msg3)) (
  status = msg1 & $xF0;
  ch = msg1 & $x0F;

  // Drop any incoming messages that match the splash note (regardless of channel)
  ((status == $x90) || (status == $x80)) && (msg2 == splashNote) ? (
    // drop incoming splash note
    0;
  ) : (
    // pass everything else through
    midisend(offset, msg1, msg2, msg3);
  );

  splash_ch != -1 ? (
    // we sent a splash - now send a noteOff for it
    midisend(offset, ($x80 | splash_ch) & $xFF, splashNote & $x7F, $x40);
    splash_ch = -1;
  );

  // if CC4
  status == $xB0 && msg2 == 4 ? (

    value = msg3 & $x7F;

    // have we seen any pedal movement at all?
    (prev_cc4 != -1) ? (

      movingTowardsClosed = (
        value < prev_cc4 && closedSide == 0) ? 1 : (
        value > prev_cc4 && closedSide == 1) ? 1 : (
        0
      );
      movingTowardsClosed ? (
        delta = (closedSide == 0) ? (
          prev_cc4 - value
        ) : (
          value - prev_cc4
        );
        // have we passed closed?
        (closedSide == 0 && prev_cc4 <= closedPoint) || (closedSide == 1 && prev_cc4 >= closedPoint) ? (
          overshoot = (closedSide == 0) ? closedPoint - prev_cc4 : prev_cc4 - closedPoint;
        ) : (
          (delta > extreme) ? extreme = delta;
        );
      );

      // have we worked out what direction the pedal is moving?
      extreme != -1 && overshoot != -1 && prev_closing == 1 && movingTowardsClosed == 0 ? (
        // we arrived, calculate velocity
        damping = (overshoot == 0 || dampScale == 0) ? 0 : overshoot * dampScale;

        splash_ch = ch;
        splash_vel = (extreme * velScale * 2);
        splash_vel = splash_vel - damping;
        splash_vel = (splash_vel > 127) ? 127 : splash_vel;
        splash_vel = (splash_vel < 1) ? 1 : splash_vel;
        overshoot == -1;
        extreme = -1;
      );

      // store latest value
      prev_closing = movingTowardsClosed;
    );

    // store latest value
    prev_cc4 = value;

    splash_ch != -1 &&
        ((closedSide == 0 && prev_cc4 > closedPoint) || (closedSide == 1 && prev_cc4 < closedPoint)) ? (
      midisend(offset, ($x90 | splash_ch) & $xFF, splashNote & $x7F, splash_vel & $x7F);
      dbgSplashVel = splash_vel;
    );
  );
);
